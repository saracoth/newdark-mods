DML1

// This file has no fingerprinting, because it's intended for use in all NewDark
// games. There may be game-specific metaproperties like J4FRadarLootPOI and so
// on, but we rely on other DML files to add them to objects (or to enable our
// scripts to do so).

// This allows us to use our .nut files.
#script "squirrel"

/*
================================================================================
== Point of Interest Metaproperties ============================================
================================================================================
*/

// This is nonfunctional on its own, but serves as a common parent type for all
// metaproperties that mark an item as interesting.
CreateArch "MetaProperty" "J4FRadarPointOfInterest"

// All archetypes from here on out are in pairs. The parent has no properties
// and serves as a flag to identify what kind of POI something is. The child
// has a script, and can be used for any needed processing.

// This is used when we don't have a more specific POI type to assign, but
// know we're interested in a thing.
CreateArch "J4FRadarPointOfInterest" "J4FRadarFallbackPOI"

CreateArch "J4FRadarFallbackPOI" "J4FRadarFallbackPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarGrabbableTarget"
		"Don't Inherit" FALSE
	}
}

// The rest are for more specific behaviors.

CreateArch "J4FRadarPointOfInterest" "J4FRadarContainerPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarContainerPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarContainerTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarDevicePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarDevicePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarDeviceTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarEquipPOI"
// More specific stuff for SS2, which has a bigger variety.
// Still, everything can have at most one equip type,
// even if multiple are assigned, so they're all created
// from the basic equipment POI.
CreateArch "J4FRadarEquipPOI" "J4FRadarEquipUnlimitedPOI"
CreateArch "J4FRadarEquipPOI" "J4FRadarEquipStackedPOI"
CreateArch "J4FRadarEquipPOI" "J4FRadarEquipSlottedPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarEquipPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarEquipTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarEquipPOI_SU"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarEquipUnlimitedTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarEquipPOI_ST"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarEquipStackTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarEquipPOI_SS"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarEquipSlottedTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarCreaturePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarCreaturePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarCreatureTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarCreatureBoxPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarCreatureBoxPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarCreatureBoxTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarLootPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarLootPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarLootTarget"
		"Don't Inherit" FALSE
	}
}

// This one is an oddball, because rather than rely on proxy items, we need
// to watch the book/scroll/etc. for frob events. We can only do that if we
// can attach a script to the item itself.
CreateArch "J4FRadarPointOfInterest" "J4FRadarReadablePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarReadablePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarReadableTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarQuestPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarQuestPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarQuestTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarSecretPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarSecretPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarSecretTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarNanitePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarNanitePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarNaniteTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarCyberModulePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarCyberModulePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarCyberModuleTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarCyberModuleTrapPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarCyberModuleTrapPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarCyberModuleTrapTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarSimpleReadablePOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarSimpleReadablePOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarSimpleReadableTarget"
		"Don't Inherit" FALSE
	}
}

CreateArch "J4FRadarPointOfInterest" "J4FRadarReadableTrapPOI"

CreateArch "J4FRadarPointOfInterest" "J4FRadarReadableTrapPOI_S"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarReadableTrapTarget"
		"Don't Inherit" FALSE
	}
}

/*
================================================================================
== Point of Interest Timer Script ==============================================
================================================================================
*/

// All timers are one-shot, not repeating forever. So if the POI target scripts
// were responsible for repeating the timer on every tick, two or more scripts
// on the same item will result in an explosion. The first timer tick hits both
// scripts, which each schedule another timer. Now we have two timers running.
// Those two ticks will result in scheduling four timers. Those four ticks will
// result in scheduling eight timers, and so on and so on.
//
// To avoid this, we rely on a separate script which is not part of our
// hierarchy of POI target logic. So this script will manage the timer, and all
// the other target scripts will listen for it.

CreateArch "MetaProperty" "J4FRadarPOITimer"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarPOIClock"
		"Don't Inherit" FALSE
	}
}

/*
================================================================================
== Radar Point-of-interest Proxies =============================================
================================================================================
*/

// There are some cases where scripts cannot be directly attached to an object
// of interest. For example, anything marked as "don't inherit" scripts. It may
// be possible to manually set a Script 1, Script 2, or Script 3 property if one
// is available, but we're trying to avoid that, both in DML and in squirrel.
// And attaching metaproperties to Don't-Inherit items will fail to attach any
// scripts from that metaproperty.
//
// The least side-effecting method, so far, is to instead create a completely
// separate object to attach scripts to. It will represent that interesting item
// to the radar system on its behalf. These proxies could also be attached to
// the original item with links, which should allow the game to destroy the
// proxy if the original object gets destroyed. That simplifies POI cleanup.

// This is the proxy object marker itself. We create one of these for every
// proxied POI item.
CreateArch "Marker" "J4FRadarProxyPOI"

/*
================================================================================
== Radar Point-of-interest Flags ===============================================
================================================================================
*/

// This non-functional metaproperty is used to flag whether an item has been
// initialized, whether by proxy or by attaching a script metaproperty directly.
// In the case of proxies, it's quicker than searching through all its links to
// see if any of the linked items happens to be a proxy, and speeds up simple
// "do I have a proxy yet?" checks.
CreateArch "MetaProperty" "J4FRadarPoiInitted"

// A variation on J4FRadarPoiInitted saying that this item needs a proxy.
CreateArch "J4FRadarPoiInitted" "J4FRadarPoiProxyInitted"

// This let us know we've seen our scripts be killed because something was
// set to Don't Inherit after we intialized it. This allows us to re-initialize
// it later, and force use of proxies.
CreateArch "MetaProperty" "J4FRadarPoiNeutered"
