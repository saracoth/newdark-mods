DML1

// Let's limit this to Thief-like games. Note that it's not necessarily
// possible to detect whether the base mod has been applied or not. If it
// adds a property to a vanilla game object, we can maybe look for that.
// But we can't check whether the base mod's new CreateArch objects exist or
// not, since we don't necessarily know what IDs they'll end up with.
FINGERPRINT
{
	NAME -2099 Garrett
}

// TODO: worst-case performance testing
+ObjProp "IsLoot" "Scripts"
{
	"Script 3" "J4FRadarLootTarget"
	"Don't Inherit" FALSE
}

/*
================================================================================
== IsLoot Handling =============================================================
================================================================================
*/

// IsLoot is a metaproperty, and actual loot items are scattered all over the
// object hierarchy. And mission authors can slap IsLoot on any old random
// thing in the mission if they want. So we want to identity items with this
// metaproperty as interesting, but for the reasons noted in the base mod, we
// shouldn't attach scripts to an existing object or metaproperty. We also can't
// give a metaproperty to a metaproperty. So instead, we're using the ping/echo
// system. When the player gets near, they will stimulate the loot. The loot
// will reflect that signal back to the player, who will then flag the loot as
// an item of interest.

++Receptron "IsLoot" "J4FRadarStim"
{
	Min 0.1
	Max None
	
	Target Source
	Agent Me

	Effect "stimulate"
	{
		"Stimulus" "J4FRadarStim"
		"Multiply By" 0.00
		"Then Add" -1.00
	}
}

/*
================================================================================
== Identified Loot =============================================================
================================================================================
*/

// Once loot has been identified, we'll flag it with this metaproperty. That
// will attach the desired script to the item and nullify the radar stim from
// now on.

CreateArch "MetaProperty" "J4FRadarLootPOI"
{
	+ObjProp "Scripts"
	{
		"Script 0" "J4FRadarLootTarget"
		"Don't Inherit" FALSE
	}
}

++Receptron "J4FRadarLootPOI" "J4FRadarStim"
{
	Min None
	Max None
	
	Target Me
	Agent Source
	
	Effect "Abort"
}
